# CMake package configuration file for SofaSimulation.
# It defines the following variables:
# - SofaFramework_INCLUDE_DIRS
# - SofaFramework_LIBRARIES

@PACKAGE_INIT@

list(FIND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}" HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH)

if(HAS_SOFAFRAMEWORK_CMAKE_MODULE_PATH EQUAL -1)
    list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()

include(CMakeSofaCreateTarget)

find_package(TinyXML REQUIRED)
sofa_create_target(TinyXML SofaFramework "${TinyXML_LIBRARIES}" "${TinyXML_INCLUDE_DIRS}")

set(SOFA_HAVE_ZLIB "@SOFA_HAVE_ZLIB@")
set(SOFA_HAVE_PNG "@SOFA_HAVE_PNG@")
set(SOFA_HAVE_GLEW "@SOFA_HAVE_GLEW@")
set(SOFA_HAVE_GLUT "@SOFA_HAVE_GLUT@")
set(SOFA_HAVE_BOOST "@SOFA_HAVE_BOOST@")
set(SOFA_HAVE_EIGEN2 "@SOFA_HAVE_EIGEN2@")
set(SOFA_NO_OPENGL "@SOFA_NO_OPENGL@")
set(SOFA_HAVE_GTEST "@SOFA_HAVE_GTEST@")

if(SOFA_HAVE_GTEST)
    find_package(GTest CONFIG REQUIRED)
    # sofa_create_target(GTest SofaFramework "${GTEST_LIBRARY}" "${GTEST_INCLUDE_DIR}")
    # sofa_create_target(GTestMain SofaFramework "${GTEST_MAIN_LIBRARY}" "${GTEST_INCLUDE_DIR}")
endif()

if(SOFA_HAVE_ZLIB)
    find_package(ZLIB REQUIRED)
    sofa_create_target(ZLIB SofaFramework "${ZLIB_LIBRARIES}" "${ZLIB_INCLUDE_DIRS}")
endif()

if(SOFA_HAVE_PNG)
    find_package(PNG REQUIRED)
    sofa_create_target(PNG SofaFramework "${PNG_LIBRARY}" "${PNG_INCLUDE_DIRS}")
endif()

if(SOFA_HAVE_GLEW)
    find_package(GLEW REQUIRED)
    sofa_create_target(GLEW SofaFramework "${GLEW_LIBRARIES}" "${GLEW_INCLUDE_DIRS}")
endif()

if(SOFA_HAVE_GLUT)
    find_package(GLUT REQUIRED)
    sofa_create_target(GLUT SofaFramework "${GLUT_glut_LIBRARY}" "${GLUT_INCLUDE_DIR}")
endif()

if(SOFA_HAVE_BOOST)
    find_package(Boost QUIET REQUIRED COMPONENTS thread system date_time chrono)
    sofa_create_target(BoostSystem SofaFramework "${Boost_SYSTEM_LIBRARY}" "${Boost_INCLUDE_DIRS}")
    sofa_create_target(BoostThread SofaFramework "${Boost_THREAD_LIBRARY}" "${Boost_INCLUDE_DIRS}")
    sofa_create_target(BoostDateTime SofaFramework "${Boost_DATE_TIME_LIBRARY}" "${Boost_INCLUDE_DIRS}")
    sofa_create_target(BoostChrono SofaFramework "${Boost_CHRONO_LIBRARY}" "${Boost_INCLUDE_DIRS}")
endif()

if(NOT TARGET SofaCore)
	include("${CMAKE_CURRENT_LIST_DIR}/SofaFrameworkTargets.cmake")
endif()

check_required_components(SofaHelper SofaDefaultType SofaCore)

set(SofaFramework_LIBRARIES SofaHelper SofaDefaultType SofaCore)
set(SofaFramework_INCLUDE_DIRS "@PACKAGE_SOFAFRAMEWORK_INCLUDE_DIRS@")
