################################################
# YOU SHOULD NOT NEED TO MODIFY ANYTHING BELOW #
################################################

# Auto-detect SOFA_DIR if not specified
isEmpty(SOFA_DIR) {
  win32 {
    SOFA_DIR = $$system(echo %CD%)
  }
  else:macx {
    SOFA_DIR = $$(PWD)
  }
  else {
    SOFA_DIR = $$system(pwd)
  }
}

DLLDESTDIR = $$SOFA_DIR/bin

# Auto-detect Qt 4.x
# The QT variable was introduced in Qt 4
!isEmpty(QT): DEFINES += SOFA_QT4

exists(sofa-local.cfg) {
  include(sofa-local.cfg)
}
else {
  # Copy sofa-default.cfg to sofa-local.cfg and modify it to set local options
  include(sofa-default.cfg)
}

!contains(DEFINES, SOFA_DEV): message("WARNING: SOFA_DEV not defined, in-development code will be disabled!")

CONFIG -= qt

# QT4: for subdirs projects, parallel makes support
CONFIG += ordered

contains(DEFINES, SOFA_QT4) {
  # qmake 4.x supports this syntax
  win32:!build_pass {
    # Force release mode when generating visual studio solutions
    CONFIGDEBUG = release
  } else:contains (CONFIG, debug, debug|release) {
    CONFIGDEBUG = debug
  } else {
#  contains (CONFIG, release, debug|release) {
    CONFIGDEBUG = release
  }
}
else {
  contains (CONFIG, debug) {
    CONFIGDEBUG = debug
  }
  contains (CONFIG, release) {
    CONFIGDEBUG = release
  }
}
contains (QMAKE_CXXFLAGS, -pg) {
  CONFIGDEBUG *= profile
}

*-32*|win32* {
  # message("32 bits target")
  TARGET_ARCH=32
} else {
  *-64*|win64* {
    # message("64 bits target")
    TARGET_ARCH=64
  } else {
    contains(QMAKE_HOST.arch, x86_64) {
      # message("64 bits target (host default)")
      TARGET_ARCH=64
    } else {
      # message("32 bits target (host default)")
      TARGET_ARCH=32
    }
  }
}

win32{
	CONFIG -= flat # preserve directory structure in files lists

	# No support for DLL yet
	CONFIGLIBRARIES=staticlib

	#CONFIGLIBRARIES=dll
	#MSVCPROJ_LIBOPTIONS=/IMPLIB:$(TargetDir)$(TargetName).lib

	# release libs have no suffix
	LIBSUFFIX=
	
	contains (CONFIGDEBUG, debug) {
		LIBSUFFIX = d
	}

	# Config for Visual C++ 7
	win32-msvc.net{
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC7
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT	
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC7
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD
		}
		QMAKE_CXXFLAGS_DEBUG = -wd4675 -wd4250 -wd4251 -wd4275 -GR -GX -MDd -Od -ZI -Gm /MP
		QMAKE_CXXFLAGS_RELEASE = -wd4675 -wd4250 -wd4251 -wd4275 -GR -GX -MD -O2 /MP
	}
	
	# Config for Visual C++ 8
	win32-msvc2005{
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC8
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT	
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC8
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD
		}
		QMAKE_CXXFLAGS_DEBUG = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MDd -Od -ZI -Gm /MP
		QMAKE_CXXFLAGS_RELEASE = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MD -O2 /MP
	}

	# Config for Visual C++ 9
	win32-msvc2008{
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win32/DebugVC9
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win32/ReleaseVC9
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD 
			QMAKE_CFLAGS += /MP
		}
		QMAKE_CXXFLAGS_DEBUG = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MDd -Od -ZI /MP 
		QMAKE_CXXFLAGS_RELEASE = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MD -O2 /MP -D_SECURE_SCL=0 -D_ITERATOR_DEBUG_LEVEL=0
	}
	
	win64-msvc2008{
	
		CONFIGPROJECT=console vc7 thread
		contains (CONFIGDEBUG, debug) {
			LIBSDIRECTORY=win64/DebugVC9
			QMAKE_LFLAGS += /NODEFAULTLIB:libcd /NODEFAULTLIB:MSVCRT /MACHINE:x64
		}
		contains (CONFIGDEBUG, release) {
			LIBSDIRECTORY=win64/ReleaseVC9
			QMAKE_LFLAGS += /NODEFAULTLIB:libc /NODEFAULTLIB:MSVCRTD  /MACHINE:x64
		}
		RCC_DIR = OBJECTS_DIR
		QMAKE_CXXFLAGS_DEBUG = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MDd -Od -Zi  /MP
		QMAKE_CXXFLAGS_RELEASE = -wd4996 -wd4250 -wd4251 -wd4275 -EHsc -MD /O2 /MP -D_SECURE_SCL=0 -D_ITERATOR_DEBUG_LEVEL=0
	}


	
	# Config for MinGW
	win32-g++{
		CONFIGPROJECT=console thread
		LIBSDIRECTORY=win32/gcc
	}

	INCLUDEPATH *= $$SOFA_DIR/include/win32
}
unix {
	macx {
		QMAKE_MACOSX_DEPLOYMENT_TARGET=10.4
		QMAKE_LFLAGS += -L/System/Library/Frameworks/
		
		LIBSDIRECTORY=macx

#		CONFIGLIBRARIES=staticlib 
		CONFIGLIBRARIES=dynamiclib
#		QMAKE_LFLAGS_SONAME = -install_name$${LITERAL_WHITESPACE}@rpath/
#		QMAKE_LFLAGS += -Wl,-rpath,@loader_path
#		QMAKE_LFLAGS += -Wl,-rpath,@executable_path/../lib/macx
#		QMAKE_LFLAGS += -Wl,-rpath,@executable_path/../../../../lib/macx

		contains (CONFIGDEBUG, debug) {
			LIBSUFFIX = d
		}

		BUNDLE_RESOURCES_SHADERS.path = Contents/Resources/shaders
#		BUNDLE_RESOURCES_SHADERS.files = $$SOFA_DIR/share/shaders/*
		BUNDLE_RESOURCES_SHADERS.files = $$system(ls share/shaders)
		BUNDLE_RESOURCES_SHADERS.files = $$join(BUNDLE_RESOURCES_SHADERS.files,' $$SOFA_DIR/share/shaders/','$$SOFA_DIR/share/shaders/','')
		APP_BUNDLE_DATA += BUNDLE_RESOURCES_SHADERS
		BUNDLE_RESOURCES_TEXTURES.path = Contents/Resources/textures
#		BUNDLE_RESOURCES_TEXTURES.files = $$SOFA_DIR/share/textures/*
		BUNDLE_RESOURCES_TEXTURES.files = share/textures/SOFA_logo.bmp $$system(ls share/textures/media*)
		BUNDLE_RESOURCES_TEXTURES.files = $$join(BUNDLE_RESOURCES_TEXTURES.files,' $$SOFA_DIR/','$$SOFA_DIR/','')
		APP_BUNDLE_DATA += BUNDLE_RESOURCES_TEXTURES
	}
 	
	!macx {
		LIBSDIRECTORY=linux

		# Use shared library unless we are profiling the code
		contains (CONFIGDEBUG, profile) {
			CONFIGSTATIC=static
			CONFIGLIBRARIES=staticlib
		}
		else {
			# Use shared libraries unless we asked for a static compilation
			contains (CONFIGSTATIC, static) {
				CONFIGLIBRARIES=staticlib
			}
			else {
				CONFIGLIBRARIES=dynamiclib
			}
		}

		contains (CONFIGDEBUG, debug) {
			LIBSUFFIX = d
		}

		# Check that all symbols are resolved when building shared libraries
		#QMAKE_LFLAGS *= -Wl,--no-allow-shlib-undefined
		QMAKE_LFLAGS *= -Wl,-z,defs

		# Allow multiple define for building binaries with static libraries
		contains(CONFIGSTATIC, static) {
			contains(TEMPLATE,app) {
				QMAKE_LFLAGS *= -Wl,-z,muldefs
			}
		}
	}
}



!contains (CONFIGDEBUG, debug) {
	DEFINES += NDEBUG
}

contains (CONFIGDEBUG, debug) {
	SUFFIX = d
}

contains (QMAKE_CXXFLAGS, -pg) {
	SUFFIX = $${SUFFIX}p
	LIBSUFFIX = $${LIBSUFFIX}
}

# Class reflection / plugins system: define SOFA_TARGET macro
!contains(TEMPLATE,subdirs) {
#isEmpty(TARGET): message("WARNING: TARGET should be defined BEFORE including sofa.cfg")
#!isEmpty(TARGET): message("TARGET: $$TARGET")
DEFINES *= SOFA_TARGET=$$TARGET
}

MYSUFFIX = ""
contains(TEMPLATE, app) {
  MYSUFFIX = $$SUFFIX
}
contains(TEMPLATE, vcapp) {
  MYSUFFIX = $$SUFFIX
}
contains(TEMPLATE, lib) {
  MYSUFFIX = $$LIBSUFFIX
}
contains(TEMPLATE, vclib) {
  MYSUFFIX = $$LIBSUFFIX
}
!isEmpty(MYSUFFIX) {
  TARGET=$$TARGET$$MYSUFFIX
}

OBJECTS_DIR = OBJ/$$join(CONFIGDEBUG,,,)

CONFIGLIBRARIES+= \
                  warn_on \
                  stl \
                  rtti \
                  thread

CONFIGPROJECT += warn_on
CONFIGPROJECTGUI = $$CONFIGPROJECT
CONFIGPROJECTCMD = $$CONFIGPROJECT
CONFIGPROJECTCMD += console

INCLUDEPATH *= \
               $$SOFA_DIR/include \
               $$SOFA_DIR/framework \
               $$SOFA_DIR/modules \
               $$SOFA_DIR/applications

DEPENDPATH  *= \
               $$SOFA_DIR/framework \
               $$SOFA_DIR/modules \
               $$SOFA_DIR/applications

QMAKE_LIBDIR = $$SOFA_DIR/lib/$$LIBSDIRECTORY 
QMAKE_LIBDIR += $$SOFA_DIR/lib/sofa-plugins

!macx: QMAKE_LIBDIR += $$SOFA_DIR/lib/$$LIBSDIRECTORY/../Common

win32-gcc: QMAKE_LIBDIR += $$SOFA_DIR/bin

# List of libs to link with

contains(DEFINES,SOFA_DEV){ # BEGIN SOFA_DEV
#SOFA_EXT_LIBS *= -lSLC$$LIBSUFFIX
} # END SOFA_DEV


win32 {
	SOFA_EXT_LIBS *= -lglut32 -lcomctl32 -lopengl32 -lglu32 -lAdvAPI32 -lUser32 -lShell32 -lGdi32 -lWSock32 -lWS2_32 -lOle32
}

unix {
	macx : SOFA_EXT_LIBS += -lpthread -framework OpenGL -framework GLUT
	!macx : SOFA_EXT_LIBS *= -L/usr/X11R6/lib -lglut -lGL -lGLU -lpthread -lX11
}

########################################################################
#  BOOST
########################################################################
contains(DEFINES,SOFA_HAVE_BOOST){
  SOFA_EXT_LIBS *= -lboost_thread -lboost_system
}else{
  # use miniBoost headers included in extlibs
  INCLUDEPATH *= $$SOFA_DIR/extlibs/miniBoost
}

########################################################################
#  CSPARSE
########################################################################
contains(DEFINES,SOFA_HAVE_CSPARSE){
  SOFA_EXT_LIBS *= -lcsparse$$LIBSUFFIX
  # use csparse headers included in extlibs
  INCLUDEPATH *= $$SOFA_DIR/extlibs/csparse
}

########################################################################
#  TinyXML (default parser)
########################################################################

!contains(DEFINES,SOFA_XML_PARSER_LIBXML){
  DEFINES *= SOFA_XML_PARSER_TINYXML
}

# GraphVisitor.h includes tinyxml headers even if it is not the active parser in SOFA
INCLUDEPATH *= $$SOFA_DIR/extlibs/tinyxml

contains(DEFINES,SOFA_XML_PARSER_TINYXML){
#  DEFINES *= TIXML_USE_STL
  SOFA_EXT_LIBS += -ltinyxml$$LIBSUFFIX
}

########################################################################
#  LibXML
########################################################################

# If PML is active, we need to include libxml even if it is not the active parser in SOFA
!contains(DEFINES,SOFA_PML):!contains(DEFINES,SOFA_XML_PARSER_LIBXML){
}
else{
  win32 {
    SOFA_EXT_LIBS *= -llibxml2 
  }
  unix {
    # Use xml2-config if available to get options for libxml2
    QMAKE_CXXFLAGS *= $$system(xml2-config --cflags 2>/dev/null || echo '-I/usr/include/libxml2')
    SOFA_EXT_LIBS *=  $$system(xml2-config --libs 2>/dev/null || echo '-lxml2 -lz')
    # TODO: in which case the following is still necessary ?
    INCLUDEPATH *= /usr/include/libxml2
  }
}

########################################################################
#  Eigen
########################################################################
#You need to install Eigen2: http://eigen.tuxfamily.org/
contains(DEFINES,SOFA_HAVE_EIGEN2){
	INCLUDEPATH *= $$SOFA_DIR/extlibs/eigen-3.0-beta2/
!contains (CONFIGDEBUG, debug) {
		DEFINES *= EIGEN_NO_DEBUG
}
}

########################################################################
#  ZLIB
########################################################################
contains(DEFINES,SOFA_HAVE_ZLIB){
	win32 {
		SOFA_EXT_LIBS *= -lzlib
	}
	unix {
		macx : SOFA_EXT_LIBS *= -lz
		!macx : SOFA_EXT_LIBS *= -lz
	}
}

########################################################################
#  LibPNG
########################################################################
contains(DEFINES,SOFA_HAVE_PNG){
	win32 {
		SOFA_EXT_LIBS *= -llibpng -lzlib
	}
	unix {
		macx : SOFA_EXT_LIBS *= -lpng12 -lz 
		!macx : SOFA_EXT_LIBS *= -lpng -lz 
	}
}

########################################################################
#  NewMAT
########################################################################

INCLUDEPATH *= $$SOFA_DIR/extlibs/newmat
SOFA_EXT_LIBS *=  -lnewmat$$LIBSUFFIX

########################################################################
#  METIS
########################################################################

contains(DEFINES,SOFA_HAVE_METIS){
	SOFA_EXT_LIBS *= -lmetis
}

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_EXTLIBS_METIS){
	DEFINES *= SOFA_HAVE_METIS
	SOFA_EXT_LIBS -= -lmetis
	SOFA_EXT_LIBS *= -lmetis$$LIBSUFFIX
	INCLUDEPATH *= $$SOFA_DIR/extlibs/metis/Lib
}
} # END SOFA_DEV

########################################################################
#  TAUCS
########################################################################

contains(DEFINES,SOFA_HAVE_TAUCS){
	SOFA_EXT_LIBS *= -ltaucs
}

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_EXTLIBS_TAUCS){
	DEFINES *= SOFA_HAVE_TAUCS
	SOFA_EXT_LIBS -= -ltaucs
	SOFA_EXT_LIBS *= -ltaucs$$LIBSUFFIX

	contains(DEFINES,SOFA_HAVE_CILK){
		TAUCS_DIR = $$SOFA_DIR/extlibs/taucs
	} else {
		TAUCS_DIR = $$SOFA_DIR/extlibs/taucs-svn
	}

	INCLUDEPATH *= $$TAUCS_DIR/src
	INCLUDEPATH *= $$TAUCS_DIR/external/COLAMD/Include
	win32 {
		INCLUDEPATH *= $$TAUCS_DIR/build/win32
	}
	unix {
		macx: INCLUDEPATH *= $$TAUCS_DIR/build/macx
		!macx: INCLUDEPATH *= $$TAUCS_DIR/build/linux
	}
}
} # END SOFA_DEV

########################################################################
#  CILK
########################################################################

contains(DEFINES,SOFA_HAVE_CILK){
	SOFA_EXT_LIBS *= -lcilk
}

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_HAVE_CILK){
	DEFINES *= SOFA_HAVE_CILK
	INCLUDEPATH *= /usr/local/include/cilk
}
} # END SOFA_DEV


########################################################################
#  CGAL
########################################################################

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_HAVE_CGAL){
	SOFA_CGAL_PATH = $$SOFA_DIR/extlibs/CGAL
	
	SOFA_EXT_LIBS *= -lCGAL
	
	SOFA_EXT_LIBS *= -lgmp -lmpfr -lboost_thread-mt
	
	INCLUDEPATH *= $$SOFA_CGAL_PATH/include
}
} # END SOFA_DEV

########################################################################
#  FFMPEG
########################################################################

contains(DEFINES,SOFA_HAVE_FFMPEG){
	SOFA_EXT_LIBS *= -lavformat -lavcodec -lavutil -lswscale
}

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_EXTLIBS_FFMPEG){
	DEFINES *= SOFA_HAVE_FFMPEG
	SOFA_EXT_LIBS -= -lavformat -lavcodec -lavutil -lswscale
	SOFA_EXT_LIBS *= -lavformat$$LIBSUFFIX -lavcodec$$LIBSUFFIX -lavutil$$LIBSUFFIX -lswscale$$LIBSUFFIX
	
	INCLUDEPATH *= $$SOFA_DIR/extlibs/ffmpeg
}
} # END SOFA_DEV


########################################################################
#  OpenCV
########################################################################

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
contains(DEFINES,SOFA_HAVE_OPENCV){
	//SOFA_OPENCV_PATH = $$SOFA_DIR/extlibs/OpenCV
	INCLUDEPATH *= $$SOFA_OPENCV_PATH/include
	SOFA_EXT_LIBS *= -L$$SOFA_OPENCV_PATH/lib
	unix{
		SOFA_EXT_LIBS *=-lcv$$LIBSUFFIX -lcvaux$$LIBSUFFIX -lhighgui$$LIBSUFFIX -lcxcore$$LIBSUFFIX
	}
	win32{
		SOFA_EXT_LIBS *=-lcv210$$LIBSUFFIX -lcvaux210$$LIBSUFFIX -lhighgui210$$LIBSUFFIX -lcxcore210$$LIBSUFFIX
	}
}
} # END SOFA_DEV


########################################################################
#  GLEW
########################################################################
contains(DEFINES,SOFA_HAVE_GLEW){
	win32 {
		SOFA_EXT_LIBS *= -lglew32
	}
	unix {
		macx : SOFA_EXT_LIBS *= -lGLEW
		!macx : SOFA_EXT_LIBS *= -lGLEW
	}
}

########################################################################
# GLUT
########################################################################
contains (DEFINES, SOFA_GUI_GLUT) {
	SOFA_GUI_LIBS *= -lsofaguiglut$$LIBSUFFIX
}

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
########################################################################
# FLTK
########################################################################
contains (DEFINES, SOFA_GUI_FLTK) {
	SOFA_GUI_LIBS *= -lsofaguifltk$$LIBSUFFIX
	win32 {
		SOFA_EXT_LIBS *= -lfltk -lfltkgl
	}
	unix {
		SOFA_EXT_LIBS *= -lfltk_gl -lfltk
	}
}
} # END SOFA_DEV

########################################################################
# QTVIEWER
########################################################################
contains (DEFINES, SOFA_GUI_QTVIEWER) {
	DEFINES *= SOFA_GUI_QT
}

########################################################################
# QGLVIEWER
########################################################################
contains (DEFINES, SOFA_GUI_QGLVIEWER) {
	DEFINES *= SOFA_GUI_QT
	INCLUDEPATH *= $$SOFA_DIR/extlibs/libQGLViewer-2.3.3/
    SOFA_GUI_EXT_LIBS *= -lQGLViewer$${LIBSUFFIX}
}

########################################################################
# QT
########################################################################
contains (DEFINES, SOFA_GUI_QT) {
	SOFA_GUI_LIBS *= -lsofaguiqt$$LIBSUFFIX
	macx: SOFA_GUI_EXT_LIBS += -framework QtCore -framework QtXml -framework QtSql -framework QtNetwork
	CONFIGPROJECTGUI += qt
	contains (DEFINES, SOFA_QT4) {	
	  QT *= opengl qt3support
	}
	else {	
	  QT *= opengl	
	}
	INCLUDEPATH += $$SOFA_DIR/extlibs/qwt-5.2.0/src/
	SOFA_GUI_EXT_LIBS *= -lqwt$${LIBSUFFIX}
}

########################################################################
# PML
########################################################################
contains(DEFINES,SOFA_PML){
	INCLUDEPATH += $$SOFA_DIR/extlibs/PML
	INCLUDEPATH += $$SOFA_DIR/extlibs/PML/PhysicalProperties
	INCLUDEPATH += $$SOFA_DIR/extlibs/LML
	SOFA_EXT_LIBS *= -lphysicalmodel$$LIBSUFFIX -lload$$LIBSUFFIX
	SOFA_MODULES_LIBS *= -lsofapml$$LIBSUFFIX
}
########################################################################
# SOFA-PARALLEL
########################################################################
contains (DEFINES, SOFA_SMP) {
  DEFINES += SOFA_HAVE_KAAPI
}
contains (DEFINES, SOFA_HAVE_KAAPI) {
	SOFA_EXT_LIBS *= -lkaapi 
#	SOFA_MODULES_LIBS *= -lsofasmp$$LIBSUFFIX
	unix {
		# auto-detect KAAPI path
		KAAPI_DIR = $${system(which karun | sed 's,/bin/*karun$,,')}
		isEmpty (KAAPI_DIR){
			error( KAAPI bin dir must be reachable in $PATH)
		}
		INCLUDEPATH += $${KAAPI_DIR}/include/kaapi
		QMAKE_LIBDIR += $${KAAPI_DIR}/lib
	}
}

########################################################################
# MUPARSER
########################################################################
contains(DEFINES,MUPARSER){
	INCLUDEPATH += $$SOFA_DIR/extlibs/muparser/include
}

########################################################################
# FISHPACK
########################################################################
contains (DEFINES, SOFA_HAVE_FISHPACK) {
	SOFA_MODULES_LIBS *= -lfftpack -lfishpack
	INCLUDEPATH += $$SOFA_DIR/extlibs/fftpack $$SOFA_DIR/extlibs/fishpack
	fftpack.input = FFTPACK_SOURCES
	fishpack.input = FISHPACK_SOURCES
	QMAKE_EXTRA_UNIX_COMPILERS *= fftpack fishpack
	contains(FISHPACK_FLAGS,intel) {
		fftpack.output = ${QMAKE_FILE_BASE}.o
		fftpack.clean = ${QMAKE_FILE_OUT}

		#fishpack.depends=-lfftpack
		fishpack.output = ${QMAKE_FILE_BASE}.o
		fishpack.clean = ${QMAKE_FILE_OUT}
	}
	else {
		fftpack.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_fftpack.obj
		fftpack.clean = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_fftpack.obj
	}
	fftpack.commands = gfortran -c  ${QMAKE_FILE_NAME}
	fftpack.dependency_type = TYPE_C

	fishpack.commands = gfortran -c  ${QMAKE_FILE_NAME}
	fishpack.dependency_type = TYPE_C
}

########################################################################
# CUDA
########################################################################
contains (DEFINES, SOFA_GPU_CUDA) {

	contains(DEFINES,SOFA_GPU_CUDPP){
        contains(DEFINES,SOFA_DEV){ # BEGIN SOFA_DEV
		    INCLUDEPATH += $$SOFA_DIR/extlibs/cudpp/cudpp/include
		    INCLUDEPATH += $$SOFA_DIR/extlibs/cudpp/cudpp/src
		    INCLUDEPATH += $$SOFA_DIR/extlibs/cudpp/common/inc
        } # END SOFA_DEV
		SOFA_EXT_LIBS *= -lcudpp$$LIBSUFFIX
	}

	SOFA_MODULES_LIBS *= -lsofagpucuda$$LIBSUFFIX
	cuda.input = CUDA_SOURCES
	QMAKE_EXTRA_UNIX_COMPILERS *= cuda
	win32 {
		ENV_CUDA_INC_PATH = $$system(echo %CUDA_INC_PATH%)
		CUDA_BIN_PATH = $$system(echo %CUDA_BIN_PATH%)
	    CUDA_DIR = $$dirname(ENV_CUDA_INC_PATH)
		INCLUDEPATH += $${CUDA_DIR}/include
	    win64-msvc*{
			QMAKE_LIBDIR += $${CUDA_DIR}/lib64
		}
		win32-msvc*{
			QMAKE_LIBDIR += $${CUDA_DIR}/lib
			CUDA_FLAGS += --machine 32
		}		
		SOFA_EXT_LIBS *= -lcudart
		contains(DEFINES,SOFA_GPU_CUBLAS) {
			SOFA_EXT_LIBS *= -lcublas
		}	
		contains(CUDA_FLAGS,--device-emulation) {
			cuda.output = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_cudaemu.obj
			cuda.clean = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_cudaemu.obj
		}
		else {
			cuda.output = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_cuda.obj
			cuda.clean = $${OBJECTS_DIR}/${QMAKE_FILE_BASE}_cuda.obj
		}
		cuda.commands = $$(CUDA_BIN_PATH)/nvcc.exe -c $$CUDA_FLAGS -Xcompiler -nologo $${join(INCLUDEPATH,'" -I "','-I "','"')} $${join(DEFINES," -D","-D",)} ${QMAKE_FILE_NAME} -o ${QMAKE_FILE_OUT}
		cuda.dependency_type = TYPE_C
	}
	unix {
		# auto-detect CUDA path
		CUDA_DIR = $${system(which nvcc | sed 's,/bin/*nvcc$,,')}
		INCLUDEPATH += $${CUDA_DIR}/include

		contains(TARGET_ARCH, 64) {
			QMAKE_LIBDIR += $${CUDA_DIR}/lib64
		} else {
			QMAKE_LIBDIR += $${CUDA_DIR}/lib
		}

		SOFA_EXT_LIBS *= -lcudart
		contains(DEFINES,SOFA_GPU_CUBLAS) {
			SOFA_EXT_LIBS *= -lcublas
		}
#		macx: QMAKE_LFLAGS += -Wl,-rpath,$${CUDA_DIR}/lib
		contains(CUDA_FLAGS,--device-emulation) {
			cuda.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cudaemu.o
			cuda.clean = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cudaemu.o
		}
		else {
			cuda.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.o
			cuda.clean = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.o
		}
		contains(CONFIGDEBUG, debug) {
			CUDA_FLAGS *= -g -G
		}
		contains(QMAKE_CFLAGS,-m32): CUDA_FLAGS *= -m32
		contains(QMAKE_CFLAGS,-m64): CUDA_FLAGS *= -m64
		contains(CONFIG,x86): CUDA_FLAGS *= -m32
		contains(CONFIG,x86_64): CUDA_FLAGS *= -m64
		cuda.commands = nvcc -c $$CUDA_FLAGS -Xcompiler -fPIC,$${join(QMAKE_CXXFLAGS_RELEASE,",")}  $${join(DEFINES," -D","-D")} $(INCPATH) ${QMAKE_FILE_NAME} -o ${QMAKE_FILE_OUT}
		cuda.dependency_type = TYPE_C
#		macx: cuda.commands += --host-compilation 'C'
	}
}


########################################################################
# OPENCL
########################################################################
contains (DEFINES, SOFA_GPU_OPENCL) {
	SOFA_MODULES_LIBS *= -lsofagpuopencl$$LIBSUFFIX
	
	win32 {
	}
	unix {
		macx{
        	        SOFA_EXT_LIBS += -framework OpenCL
			INCLUDEPATH += /Developer/SDKs/MacOSX10.6.sdk/System/Library/Frameworks/OpenCL.framework/Headers
		}
		else {
		# auto-detect OPENCL path
		INCLUDEPATH += $${SOFA_GPU_OPENCL_DIR}/include
		QMAKE_LIBDIR += $${SOFA_GPU_OPENCL_DIR}/lib

		SOFA_EXT_LIBS *= -lOpenCL
		}
	}
}


########################################################################
#  FLOWVR
########################################################################
contains(DEFINES,SOFA_HAVE_FLOWVR){
	FLOWVR = $(FLOWVR_PREFIX)
	QMAKE_CXXFLAGS += `pkg-config --cflags flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LDFLAGS += `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LFLAGS_DEBUG+= `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
	QMAKE_LFLAGS_RELEASE+= `pkg-config --libs flowvr-mod flowvr-ftl flowvr-render`
}
else {
	SOFA_EXT_LIBS += -lminiFlowVR$$LIBSUFFIX
	INCLUDEPATH *= $$SOFA_DIR/extlibs/miniFlowVR/include
	DEFINES *= MINI_FLOWVR
}

########################################################################
#  MKL
########################################################################
contains(DEFINES,SOFA_HAVE_MKL){
	INCLUDEPATH += $$SOFA_DIR/extlibs/MKL
	INCLUDEPATH += "$$MKL_PATH/include"
	QMAKE_LIBDIR += "$$MKL_PATH/ia32/lib"
	SOFA_EXT_LIBS *= -lmkl_c -lmkl_lapack -llibguide40
}

########################################################################
#  SENSABLE PHANTOM
########################################################################
contains(DEFINES,SOFA_HAVE_SENSABLE){
win32{
   3DTOUCH_BASE = $$system( echo %3DTOUCH_BASE% )
#  3DTOUCH_BASE = C:\Program Files\SensAble\3DTouch
  INCLUDEPATH += "$${3DTOUCH_BASE}/include"
  INCLUDEPATH += "$${3DTOUCH_BASE}/utilities/include"
#  SOFA_EXT_LIBS += -lhd -lhdu -lhl
  win32-msvc*{
	QMAKE_LIBDIR += "$${3DTOUCH_BASE}/lib/win32/"
    contains (CONFIGDEBUG, debug) {
    QMAKE_LIBDIR += "$${3DTOUCH_BASE}/utilities/lib/Win32/Debug/"
	}
	else {
    QMAKE_LIBDIR += "$${3DTOUCH_BASE}/utilities/lib/Win32/Release/"
	}
  }
  win64-msvc*{
	QMAKE_LIBDIR += "$${3DTOUCH_BASE}/lib/x64/"
    contains (CONFIGDEBUG, debug) {
    QMAKE_LIBDIR += "$${3DTOUCH_BASE}/utilities/lib/x64/Debug/"
	}
	else {
    QMAKE_LIBDIR += "$${3DTOUCH_BASE}/utilities/lib/x64/Release/"
	}
  }
}
unix{
  SOFA_EXT_LIBS += -lHL -lHD -lHLU -lHDU -lPHANToMIO
}
}

contains(DEFINES,SOFA_DEV){ # BEGIN SOFA_DEV
########################################################################
#  COLLADA DOM
########################################################################
contains(DEFINES,SOFA_HAVE_COLLADADOM){
	SOFA_EXT_LIBS *= -lcolladadom$$LIBSUFFIX
	#DEFINES *= DOM_INCLUDE_LIBXML DOM_DYNAMIC
	DEFINES *= DOM_DYNAMIC
	INCLUDEPATH *= $$SOFA_DIR/extlibs/colladadom/dom/include
	INCLUDEPATH *= $$SOFA_DIR/extlibs/colladadom/dom/include/1.4
	#INCLUDEPATH *= $$SOFA_DIR/extlibs/colladadom/dom/external-libs/pcre
}
} # END SOFA_DEV

########################################################################
#  VRPN
########################################################################
contains(DEFINES,SOFA_HAVE_VRPN){
	#SOFA_EXT_LIBS *= -lvrpn_quat$$SUFFIX -lvrpn_atmellib$$SUFFIX -lvrpn_client$$SUFFIX
	INCLUDEPATH *= $$SOFA_DIR/extlibs/VRPN/
	contains(DEFINES,VRPN_USE_WIIUSE){
		#SOFA_EXT_LIBS *= -lwiiuse$$SUFFIX
		INCLUDEPATH *= $$SOFA_DIR/extlibs/wiiuse
	}
}


contains(DEFINES,SOFA_DEV){ # BEGIN SOFA_DEV

########################################################################
# indexedmap
########################################################################
contains(DEFINES,SOFA_HAVE_INDEXEDMAP){
	INCLUDEPATH += $$SOFA_DIR/extlibs/indexedmap/includes
}

} # END SOFA_DEV

########################################################################
# DCCD
########################################################################
contains(DEFINES,SOFA_HAVE_DCCD){
	INCLUDEPATH += $$SOFA_DIR/self-ccd-1.0/inc
	SOFA_EXT_LIBS *= -ldccd$$LIBSUFFIX
}



########################################################################

SOFA_FRAMEWORK_LIBS *= -lsofahelper$$LIBSUFFIX -lsofadefaulttype$$LIBSUFFIX -lsofacore$$LIBSUFFIX

SOFA_COMPONENTS_LIBS = -lsofacomponentmastersolver$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentfem$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentinteractionforcefield$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentcontextobject$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentbehaviormodel$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentengine$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentlinearsolver$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentodesolver$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentbase$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentloader$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentcontroller$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentvisualmodel$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentmass$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentforcefield$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentmapping$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentprojectiveconstraintset$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentconstraintset$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentcollision$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentmisc$$LIBSUFFIX
SOFA_COMPONENTS_LIBS += -lsofacomponentconfigurationsetting$$LIBSUFFIX

#SOFA_MODULES_LIBS *= $$SOFA_COMPONENTS_LIBS  -lsofasimulation$$LIBSUFFIX -lsofatree$$LIBSUFFIX
SOFA_MODULES_LIBS *= $$SOFA_COMPONENTS_LIBS -lsofacomponent$$LIBSUFFIX -lsofasimulation$$LIBSUFFIX -lsofatree$$LIBSUFFIX

contains (DEFINES, SOFA_DEV) { # BEGIN SOFA_DEV
SOFA_MODULES_LIBS *= -lsofabgl$$LIBSUFFIX
} # END SOFA_DEV


SOFA_GUI_LIBS *= -lsofagui$$LIBSUFFIX -lsofaguimain$$LIBSUFFIX

# When using staticlib, rebuild applications if a library has changed
contains(CONFIGLIBRARIES,staticlib){
contains(TEMPLATE,app){
	SLIBS = $$SOFA_FRAMEWORK_LIBS $$SOFA_MODULES_LIBS $$SOFA_GUI_LIBS
	win32 {
		win32-g++ {
			SLIBS ~= s|-l(.*)|$${SOFA_DIR}/lib/$${LIBSDIRECTORY}/lib\1.a|g
		} else {
			SLIBS ~= s|-l(.*)|$${SOFA_DIR}/lib/$${LIBSDIRECTORY}/\1.lib|g
		}
	}
	unix {
		SLIBS ~= s|-l(.*)|$${SOFA_DIR}/lib/$${LIBSDIRECTORY}/lib\1.a|g
	}
	POST_TARGETDEPS += $$SLIBS
}
}

SOFA_LIBS = $${SOFA_MODULES_LIBS} $${SOFA_FRAMEWORK_LIBS} $${SOFA_EXT_LIBS}

SOFA_GUI_LIBS += $${SOFA_GUI_EXT_LIBS}

# default destination 
DESTDIR = $${SOFA_DIR}/lib/$${LIBSDIRECTORY}

