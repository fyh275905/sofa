# CMake package configuration file for SofaGui

@PACKAGE_GUARD@
@PACKAGE_INIT@

set(SOFAGUI_TARGETS @SOFAGUI_TARGETS@)

set(SOFAGUI_HAVE_SOFAHEADLESSRECORDER @SOFAGUI_HAVE_SOFAHEADLESSRECORDER@)
set(SOFAGUI_HAVE_SOFAGUIQT @SOFAGUI_HAVE_SOFAGUIQT@)
set(SOFAGUIQT_HAVE_QTVIEWER @SOFAGUIQT_HAVE_QTVIEWER@)
set(SOFAGUIQT_HAVE_QGLVIEWER @SOFAGUIQT_HAVE_QGLVIEWER@)
set(SOFAGUIQT_HAVE_QT5_CHARTS @SOFAGUIQT_HAVE_QT5_CHARTS@)
set(SOFAGUIQT_HAVE_QT5_WEBENGINE @SOFAGUIQT_HAVE_QT5_WEBENGINE@)
set(SOFAGUIQT_HAVE_NODEEDITOR @SOFAGUIQT_HAVE_NODEEDITOR@)
set(SOFAGUIQT_HAVE_TINYXML @SOFAGUIQT_HAVE_TINYXML@)

# Find dependencies
find_package(SofaFramework QUIET REQUIRED)
find_package(SofaUserInteraction QUIET REQUIRED)
find_package(SofaGraphComponent QUIET REQUIRED)
find_package(SofaMiscForceField QUIET REQUIRED) # SofaGuiQt
find_package(SofaLoader QUIET REQUIRED)

if(SOFAGUI_HAVE_SOFAGUIQT)
    if(SOFAGUIQT_HAVE_QTVIEWER)
        find_package(Qt5 QUIET REQUIRED Core Gui OpenGL)
        if(SOFAGUIQT_HAVE_QT5_CHARTS)
            find_package(Qt5 QUIET REQUIRED Charts)
        endif()
        if(SOFAGUIQT_HAVE_QT5_WEBENGINE)
            find_package(Qt5 QUIET REQUIRED WebEngine WebEngineWidgets)
        endif()
    endif()
    if(SOFAGUIQT_HAVE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
    if(SOFAGUIQT_HAVE_NODEEDITOR)
        find_package(NodeEditor QUIET REQUIRED)
    endif()
    if(SOFAGUIQT_HAVE_TINYXML)
        find_package(TinyXML QUIET REQUIRED)
    endif()
endif()

foreach(target ${SOFAGUI_TARGETS})
    if(NOT TARGET ${target})
        include("${CMAKE_CURRENT_LIST_DIR}/SofaGuiTargets.cmake")
        break()
    endif()
endforeach()
if(NOT TARGET @PROJECT_NAME@)
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()
check_required_components(@PROJECT_NAME@)
